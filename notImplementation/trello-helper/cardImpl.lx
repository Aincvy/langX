

// 对 card 类进行扩展和实现
auto TrelloCard {

    // card 的 python 库对象
    _py_card;

    // 更新当前卡的数据
    updateSelf => (){
        name = _py_card.get("name").toStr();
        id = _py_card.get("id").toStr();
        description = _py_card.get("desc").toStr();

        // 评论， 自定义字段， 过期日期等。
    }


    setName => (newName){
        _py_card.get("set_name").callMethod(newName);
    }

    moveTo => ( list, board ){
        if(!list){
            return;
        }

        local listId;
        if( list is String){
            listId = list;
        } else if ( list is TrelloList){
            listId = list.id;
        } else {
            println("Unsupported list types.. ");
            return;
        }

        if(board){
            // 同时改变 board 和 list

            local boardId;
            if(board is String){
                boardId = board;
            } else if (board is TrelloBoard){
                boardId = board.id;
            } else {
                println("Unsupported board types.. ");
                return ;
            }

            _py_card.get("change_board").callMethod(boardId, listId);

        } else {
            // 只改变list
            _py_card.get("change_list").callMethod(listId);
        }
    }


    deleteCard => () {

        _py_card.get("delete").callMethod();

        if ( list ){
            list.cardList.remove(this);
            list = null;
        }
    }

    archive => (){
        local args = PythonHook::newTuple(1);
        args.setItem(0, 1);

        _py_card.get("set_closed").callMethod(args);

        if ( list ){
            list.cardList.remove(this);
            list = null;
        }
    }

}
